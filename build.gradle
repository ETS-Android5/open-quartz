buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:2.3.3"
        classpath "com.novoda:gradle-android-command-plugin:1.7.1"
        classpath "net.ltgt.gradle:gradle-errorprone-plugin:0.0.11"
        classpath "com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.7.3"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.15.0"
        classpath "com.vanniktech:gradle-android-apk-size-plugin:0.4.0"
    }
}

allprojects {
    apply plugin: "com.github.ben-manes.versions"
    apply plugin: "net.ltgt.errorprone"

    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }

    tasks.withType(JavaCompile) {
        sourceCompatibility = JavaVersion.VERSION_1_7
        targetCompatibility = JavaVersion.VERSION_1_7

        configure(options) {
            compilerArgs << "-Xlint:all"
            compilerArgs << "-Xlint:-options"
            deprecation = true
            encoding = "UTF-8"
        }
    }

    tasks.withType(Test) {
        testLogging {
            exceptionFormat "full"
        }
    }
}

configure(project(":gdk").subprojects) {
    apply plugin: "com.android.application"
    apply plugin: "android-command"
    apply plugin: "com.getkeepsafe.dexcount"
    apply plugin: "com.vanniktech.android.apk.size"

    android {
        compileSdkVersion "Google Inc.:Glass Development Kit Preview:19"
        buildToolsVersion "26.0.1"

        defaultConfig {
            minSdkVersion 19
            targetSdkVersion 26
            versionCode 1
            versionName "1.0"
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_7
            targetCompatibility JavaVersion.VERSION_1_7
        }

        signingConfigs {
            debug {
                storeFile rootProject.file("./.buildscript/debug.keystore")
                storePassword "android"
                keyAlias "androiddebugkey"
                keyPassword "android"
            }
        }

        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile("proguard-android.txt")
                proguardFile "proguard-rules.pro"
            }
        }

        lintOptions {
            abortOnError false
        }
    }
}

configure(project(":sdk").subprojects) {
    apply plugin: "com.android.application"
    apply plugin: "android-command"
    apply plugin: "com.getkeepsafe.dexcount"
    apply plugin: "com.vanniktech.android.apk.size"

    android {
        compileSdkVersion 26
        buildToolsVersion "26.0.1"

        defaultConfig {
            minSdkVersion 19
            targetSdkVersion 26
            versionCode 1
            versionName "1.0"
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_7
            targetCompatibility JavaVersion.VERSION_1_7
        }

        signingConfigs {
            debug {
                storeFile rootProject.file("./.buildscript/debug.keystore")
                storePassword "android"
                keyAlias "androiddebugkey"
                keyPassword "android"
            }
        }

        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile("proguard-android.txt")
                proguardFile "proguard-rules.pro"
            }
        }

        lintOptions {
            abortOnError false
        }
    }
}
