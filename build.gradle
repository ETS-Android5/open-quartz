buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.1'
        classpath 'com.novoda:gradle-android-command-plugin:1.5.0'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.11.3'
    }
}

allprojects {
    apply plugin: 'com.github.ben-manes.versions'

    repositories {
        jcenter()
        mavenCentral()
    }

    tasks.withType(JavaCompile) {
        sourceCompatibility = JavaVersion.VERSION_1_7
        targetCompatibility = JavaVersion.VERSION_1_7

        configure(options) {
            compilerArgs << '-Xlint:all' << '-Xlint:-options'
            deprecation = true
            encoding = 'UTF-8'
        }
    }

    tasks.withType(Test) {
        testLogging {
            exceptionFormat 'full'
        }
    }
}

configure(project(':gdk').subprojects) {
    apply plugin: 'com.android.application'
    apply plugin: 'android-command'

    android {
        // Make sure we apply the correct tools
        compileSdkVersion 'Google Inc.:Glass Development Kit Preview:19'
        buildToolsVersion '23.0.0'

        // Set 'applicationId' in AndroidManifest.xml
        defaultConfig {
            minSdkVersion 19
            targetSdkVersion 23
            versionCode 1
            versionName '1.0'
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_7
            targetCompatibility JavaVersion.VERSION_1_7
        }

        signingConfigs {
            debug {
                storeFile rootProject.file('debug.keystore')
                storePassword 'android'
                keyAlias 'androiddebugkey'
                keyPassword 'android'
            }
        }

        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            }
        }

        lintOptions {
            abortOnError false
        }
    }
}

configure(project(':sdk').subprojects) {
    apply plugin: 'com.android.application'
    apply plugin: 'android-command'

    android {
        // Make sure we apply the correct tools
        compileSdkVersion 23
        buildToolsVersion '23.0.0'
        useLibrary 'org.apache.http.legacy'

        // Set 'applicationId' in AndroidManifest.xml
        defaultConfig {
            minSdkVersion 19
            targetSdkVersion 23
            versionCode 1
            versionName '1.0'
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_7
            targetCompatibility JavaVersion.VERSION_1_7
        }

        signingConfigs {
            debug {
                storeFile rootProject.file('debug.keystore')
                storePassword 'android'
                keyAlias 'androiddebugkey'
                keyPassword 'android'
            }
        }

        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            }
        }

        lintOptions {
            abortOnError false
        }
    }
}
